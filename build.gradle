// For those who want the bleeding edge
// 2.0-SNAP needed for OSX - https://github.com/MinecraftForge/MinecraftForge/issues/2183
buildscript {
    repositories {
        jcenter()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.0-SNAPSHOT'
        classpath 'com.github.jengelman.gradle.plugins:shadow:1.2.2'
        classpath 'net.sf.proguard:proguard-gradle:5.2.1'
    }
}

plugins {
    id 'com.github.johnrengelman.shadow' version '1.2.2'
}


apply plugin: 'net.minecraftforge.gradle.forge'
/*
// for people who want stable
plugins {
    id "net.minecraftforge.gradle.forge" version "2.0.2"
}*/

def modversion = '0.2.0'
def mcversion = '1.8'
def forgeversion = ['1.8.8':    '11.15.0.1628',
                    '1.8':      '11.14.4.1563',
                    '1.7.10':   '10.13.4.1558-1.7.10'] // FG2.0?
def forgeMappings = ['1.8.8':   'snapshot_20151128',
                     '1.8':     'snapshot_20151128',
                     '1.7.10':  'stable_12']

version = "$mcversion-$modversion"
group= 'karl.codes-minecraft'
archivesBaseName = 'antispam'
def shadowPackage = "$archivesBaseName.$modversion"

minecraft {
    version = "$mcversion-${forgeversion[mcversion]}"

    runDir = 'run'

    // default -- mappings = "snapshot_20141130"
    mappings = forgeMappings[mcversion]
}

dependencies {
    compile 'com.fasterxml.jackson.core:jackson-databind:2.6.4'
    compile 'com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:2.6.4'
    testCompile 'junit:junit:4.12'
}

sourceCompatibility = 1.7
targetCompatibility = 1.7

jar {
    classifier = 'nodeps'
}

shadowJar {
    classifier = 'all'

    relocate 'com.fasterxml', shadowPackage
    relocate 'org.yaml', shadowPackage
}

reobf {
    shadowJar { useSrgSrg() }
}

task proguard(type: proguard.gradle.ProGuardTask) {
    libraryjars "${System.getProperty('java.home')}/lib/rt.jar"
    libraryjars sourceSets.main.runtimeClasspath

    dontwarn 'net.minecraft.**'
    dontwarn 'net.minecraftforge.**'

    injars tasks.shadowJar.archivePath
    outjars tasks.shadowJar.archivePath.parent + '/' + tasks.jar.archiveName.replace('-nodeps','-proguard')

    keep '@net.minecraftforge.fml.common.Mod class * {*;}'
    keep "class * implements ${shadowPackage}.jackson.databind.cfg.ConfigFeature {*;}"
    keepclassmembers 'class * {*;}'

    keepattributes()
    keepparameternames()
    dontobfuscate()
}

tasks.proguard.dependsOn reobfShadowJar
tasks.build.dependsOn proguard

artifacts {
    archives shadowJar
}

compileJava {
    options.debug = true
    options.debugOptions.debugLevel = 'source,lines,vars'
}

processResources {
    inputs.property 'version', project.version
    inputs.property 'mcversion', project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'

        // replace version and mcversion
        expand version: project.version, mcversion: project.minecraft.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

idea {
    module {
        inheritOutputDirs = true
        jdkName = '1.7'
    }

    project {
        vcs = 'Git'
    }
}
