buildscript {
    repositories {
        jcenter()
        mavenCentral()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:1.2-SNAPSHOT'
        classpath 'com.github.jengelman.gradle.plugins:shadow:1.2.2'
        classpath 'net.sf.proguard:proguard-gradle:5.2.1'
    }
}

boolean forge1 = true

if(forge1) {
    apply plugin: 'forge'
} else {
    apply plugin: 'net.minecraftforge.gradle.forge'
}

apply plugin: 'com.github.johnrengelman.shadow'


def modversion = '0.2.0'
def mcversion = '1.7.10'
def forgeversion = ['1.8.8':    '11.15.0.1628',
                    '1.8':      '11.14.4.1563',
                    '1.7.10':   '10.13.4.1558-1.7.10']
def forgeMappings = ['1.8.8':   'snapshot_20151128',
                     '1.8':     'snapshot_20151128',
                     '1.7.10':  'stable_12']

version = "$mcversion-$modversion"
group= 'karl.codes-minecraft'
archivesBaseName = 'antispam'
def shadowPackage = "$archivesBaseName.$modversion"

minecraft {
    version = "$mcversion-${forgeversion[mcversion]}"

    runDir = 'run' // eclipse?

    // default -- mappings = "snapshot_20141130"
    mappings = forgeMappings[mcversion]
}

dependencies {
    compile 'com.fasterxml.jackson.core:jackson-databind:2.6.4'
    compile 'com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:2.6.4'
    testCompile 'junit:junit:4.12'
}

sourceCompatibility = 1.7
targetCompatibility = 1.7

jar {
    classifier = 'nodeps'
}

shadowJar {
    classifier = 'all'

    if(forge1) {
        dependencies {
            include(dependency('com.fasterxml.*:.*'))
            include(dependency('org.yaml.*:.*'))
        }
    }

    relocate 'com.fasterxml', shadowPackage
    relocate 'org.yaml', shadowPackage
}

if (forge1) {
    reobf.reobf(shadowJar) { spec ->
        spec.classpath = sourceSets.main.compileClasspath
    }
} else {
    reobf {
        shadowJar {
            // FG2
            useSrgSrg()
        }
    }
}


task deleteOrig(type: Delete) {
    delete project.tasks.jar.archivePath.getPath()
}

task deleteShaded(type: Delete) {
    delete project.tasks.shadowJar.archivePath.getPath()
}

task proguard(type: proguard.gradle.ProGuardTask) {
    libraryjars "${System.getProperty('java.home')}/lib/rt.jar"
    libraryjars sourceSets.main.runtimeClasspath

    dontwarn 'net.minecraft.**'
    dontwarn 'net.minecraftforge.**'

    injars tasks.shadowJar.archivePath
    outjars tasks.shadowJar.archivePath.parent + '/' + tasks.jar.archiveName.replace('-nodeps','-proguard')

    keep '@cpw.mods.fml.common.Mod class * {*;}'
    keep "class * implements ${shadowPackage}.jackson.databind.cfg.ConfigFeature {*;}"
    keepclassmembers 'class * {*;}'

    keepattributes()
    keepparameternames()
    dontobfuscate()
}

artifacts {
    archives shadowJar
}

compileJava {
    options.debug = true
    options.debugOptions.debugLevel = 'source,lines,vars'
}

processResources {
    inputs.property 'version', project.version
    inputs.property 'mcversion', project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'

        // replace version and mcversion
        expand version: project.version, mcversion: project.minecraft.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

idea {
    module {
        inheritOutputDirs = true
        jdkName = '1.7'
    }

    project {
        vcs = 'Git'
    }
}

//tasks.proguard.dependsOn reobfShadowJar
//tasks.build.dependsOn proguard

